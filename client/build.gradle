plugins {
    id 'java'
    id 'fabric-loom' version '1.8-SNAPSHOT'
    id 'maven-publish'
//    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    // Redot dependencies
    implementation project(':commons')
    include project(':commons')

    // OpenHFT dependencies
    implementation 'net.openhft:compiler:2.26ea0'
    include 'net.openhft:compiler:2.26ea0'

    minecraft "com.mojang:minecraft:1.20.4"
    mappings "net.fabricmc:yarn:1.20.4+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.16.9"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.97.3+1.20.4"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

remapJar {
    archiveBaseName.set("tweaksuite-client")
}

jar {
    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

tasks.named("runClient") {
    jvmArgs "--add-opens", "java.base/java.lang=ALL-UNNAMED",
            "--add-opens", "jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED"
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = 'tweaksuite'
            from components.java
        }
    }

    repositories {

    }
}

tasks.test {
    useJUnitPlatform()
}